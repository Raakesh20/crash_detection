#include <WiFi.h>
#include <FirebaseESP32.h>
#include <SPI.h>
#include <nRF24L01.h>
#include <RF24.h>


#define WIFI_SSID "ssid"
#define WIFI_PASSWORD "password"


#define FIREBASE_HOST "firebase/host/url"
#define FIREBASE_API_KEY "your_api"


FirebaseData firebaseData;
FirebaseAuth auth;
FirebaseConfig config;


#define USER_EMAIL "youremail@gmail.com"
#define USER_PASSWORD "your_password"


RF24 radio(4, 5);  
const byte address[6] = "00001";

void setup() {
    Serial.begin(115200);

    
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    Serial.print("Connecting to WiFi");
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.print(".");
    }
    Serial.println("\nWiFi Connected!");

    
    config.api_key = FIREBASE_API_KEY;
    config.database_url = FIREBASE_HOST;
    auth.user.email = USER_EMAIL;
    auth.user.password = USER_PASSWORD;
    
    Firebase.begin(&config, &auth);
    Firebase.reconnectWiFi(true);

    Serial.println("Logging into Firebase...");
    while (auth.token.uid == "") {
        Serial.print(".");
        delay(500);
    }
    Serial.println("\nFirebase Login Successful!");

    
    if (!radio.begin()) {
        Serial.println("NRF24L01 NOT detected! Check wiring.");
        while (1);
    }

    radio.setChannel(90);
    radio.openReadingPipe(0, address);
    radio.setPALevel(RF24_PA_LOW);
    radio.setDataRate(RF24_250KBPS);
    radio.startListening();  

    Serial.println("Receiver Ready!");
}

void loop() {
    if (radio.available()) {
        char text[32] = "";
        radio.read(&text, sizeof(text));
        Serial.print("Received: ");
        Serial.println(text);

        
        if (Firebase.setString(firebaseData, "/accidentAlert", text)) {
            Serial.println("Data sent to Firebase successfully!");
        } else {
            Serial.println("Firebase Error: " + firebaseData.errorReason());
        }
    }
}
